load("@bazel-erlang//:bazel_erlang_lib.bzl", "app_file", "bazel_erlang_lib", "erlc")
load("@bazel-erlang//:ct.bzl", "ct_suite")
load("//:rabbitmq.bzl", "APP_VERSION")

py_binary(
    name = "codegen",
    srcs = [
        "codegen.py",
    ],
    imports = ["../../deps/rabbitmq_codegen"],
    deps = [
        "//deps/rabbitmq_codegen:amqp_codegen",
    ],
)

AMQP_SPEC_1_0 = [
    "//deps/rabbitmq_codegen:amqp-1.0/messaging.xml",
    "//deps/rabbitmq_codegen:amqp-1.0/security.xml",
    "//deps/rabbitmq_codegen:amqp-1.0/transport.xml",
    "//deps/rabbitmq_codegen:amqp-1.0/transactions.xml",
]

genrule(
    name = "generated_headers",
    srcs = AMQP_SPEC_1_0,
    outs = ["include/amqp10_framing.hrl"],
    cmd = "mkdir -p $$(dirname $@) && $(location :codegen) hrl $(SRCS) > $@",
    tools = [":codegen"],
)

genrule(
    name = "generated_sources",
    srcs = AMQP_SPEC_1_0,
    outs = ["src/amqp10_framing0.erl"],
    cmd = "mkdir -p $$(dirname $@) && $(location :codegen) erl $(SRCS) > $@",
    tools = [":codegen"],
)

app_file(
    name = "app_file",
    app_description = "Modules shared by rabbitmq-amqp1.0 and rabbitmq-amqp1.0-client",
    app_name = "amqp10_common",
    app_version = APP_VERSION,
    modules = [":beam_files"],
)

erlc(
    name = "beam_files",
    srcs = ["src/amqp10_framing0.erl"] + glob(["src/*.erl"]),
    hdrs = ["include/amqp10_framing.hrl"] + glob(["include/*.hrl"]),
    dest = "ebin",
)

bazel_erlang_lib(
    name = "bazel_erlang_lib",
    hdrs = ["include/amqp10_framing.hrl"] + glob(["include/*.hrl"]),
    app = ":app_file",
    app_name = "amqp10_common",
    beam = [":beam_files"],
    visibility = ["//visibility:public"],
)

bazel_erlang_lib(
    name = "test_bazel_erlang_lib",
    hdrs = ["include/amqp10_framing.hrl"] + glob(["include/*.hrl"]),
    app = ":app_file",
    app_name = "amqp10_common",
    beam = [":beam_files"],
    visibility = ["//visibility:public"],
)

ct_suite(
    name = "binary_generator_SUITE",
)
